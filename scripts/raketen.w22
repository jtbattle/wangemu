CLEAR
10 REM "RAKETEN" ATS 6/27/75
20 DIM A$1, R$(3)1
30%              (#)            (#)            (#)         BUNKER
40 %ABGESCHOSSEN SIND ## VON ### RAKETEN. NOCH ### ABWEHRRAKETEN...
50 SELECT P, PRINT 005(64):PRINT HEX(030A0A):PRINT TAB(19);"ABWEHR-RAKETEN ZENTRALE":PRINT
51 PRINT "ALS BEFEHLSHABER DER ZENTRALE MUESSEN SIE FEINDLICHE INTERKONTINENTAL-RAKETEN ABSCHIESSEN, BEVOR DIESE IHREN"
60 PRINT "BUNKER ZERSTOEREN. IN DREI SILOS STEHEN IHNEN ABWEHR-":PRINT"RAKETEN UNBEKANNTER REICHWEITE ZUR VERFUEGUNG...":PRINT
61 PRINT "DIE ABWEHR-RAKETEN WERDEN DURCH DIE TASTEN 1, 2, 3 GEFEUERT,"
70 PRINT "UND DURCH DIE FUNKTIONSTASTEN 00 NACH LINKS, UND 01 NACH RECHTS ABGELENKT.":PRINT
80 PRINT "BITTE DIE ZENTRALE BESETZEN, UND EINEN KNOPF DRUECKEN."
90 KEYIN A$,100,100:GOTO 90
100 A,V=0:N=1:FOR I= 1 TO 3: BIN(R$(I))=51+7*RND(1):V=V+VAL(R$(I))-48:NEXT I
110 PRINT HEX(030C0C0C): PRINTUSING 30,R$(1), R$(2), R$(3):H=0: GOTO 150
120 IF N/INT(1+4*RND(1))<>INT(N/INT(1+4*RND(1))) THEN 150:SELECT P6: PRINT HEX(01);"FEUERPAUSE...":PRINT "SILOS WERDEN REPARIERT UND NEU BESTUECKT...":SELECT P
121 FOR I=3 TO 4*RND(1) STEP -1:R=51+7*RND(1):IF R<= VAL(R$(I)) THEN 140: IF R$(I)<"1" THEN 130:V=V-VAL(R$(I))+48
130 BIN (R$(I))=R:V=V+INT(R)-48
140 NEXT I: GOTO 110
150 PRINT HEX (010C0C):SELECT P4,PRINT 205:PRINTUSING 40,A,N,V:SELECTP, PRINT005:PRINT HEX(01);
160 R=1+RND(1)*3.6:I=INT(8*RND(1))+1
170 I=I+1:FOR T=1 TO 13: IF I=T THEN 180:IF H<>T THEN 230: PRINT TAB(W);"!";:GOTO230
180 IF I<>H THEN210: IF INT(I*R)+3 <= W THEN 190:IF INT(I*R)>W THEN200:T=13:NEXT T:GOTO 310
190 PRINT TAB(I*R);">R>";TAB(W);"!";:GOTO230
200 PRINT TAB(W);"!";TAB(I*R);">R>";:GOTO 230
210 IF H<>I+1 THEN 220: IF INT(I*R)+3<=W THEN 220:IF INT(I*R)>W THEN 220: T=13:NEXT T: GOTO 310
220 PRINT TAB(I*R);">R>";
230 PRINT TAB(63):KEYIN A$,240,260:GOTO270
240 IF A$<"1" THEN 270: IF A$>"3" THEN 270:CONVERTA$TOB:IF R$(B)<"1" THEN 270:W=15*B:H=13:V=V-1:ADD(R$(B),FF):IF R$(B)>"0" THEN 250:R$(B)=" "
250 PRINT HEX(0D010C0C0C): PRINTUSING 30,R$(1),R$(2),R$(3):PRINTUSING 40,A,N,V;:PRINT HEX (0D01);:FOR B=1 TO T:PRINT :NEXT B:GOTO 270
260 IF A$>HEX(01) THEN 270:W=W-1+2*VAL(A$)
270 NEXT T: PRINT HEX(01);:H=SGN(INT(30*RND(1)))*(H-SGN(H)):IFI<7+SGN(INT(20*RND(1)))*7 THEN 170:N=N+1:IF I=14 THEN 280:PRINT HEX(01);"VERSAGER...":GOTO 120
280 R=INT(I*R):FOR I=1 TO 3:IF ABS(R-I*15+1)<=1 THEN 290:NEXT I:IF ABS(R-57)<3 THEN330:GOTO 120
290 IF R$(I)<"1" THEN 300:V=V-VAL(R$(I))+48:R$(I)="%":PRINT HEX(01);"SILO";I;"ZERSTOERT...";HEX(0C0C0C0C0C0C):SELECT P6:PRINT TAB(15*I-2);HEX(5C0A5C0A250C2F0C2F08080821080A21)
300 I=3:NEXTI:GOTO120
310 A=A+1:PRINTHEX(01);"ABSCHUSS...":H=H:N=N+1
320 PRINT HEX(01);:FOR T=1 TO I: PRINT:NEXT T: SELECT PRINT 005(255),P0: FOR T=1 TO R*I: PRINT HEX(09);: NEXT T:PRINT HEX(2F215C0C080808082D3F2A3F2D0C080808085C212F)
321 SELECT P,PRINT 005(64):PRINT HEX(0D010C0C0C):PRINTUSING 30,R$(1),R$(2),R$(3):GOTO 120
330 PRINT HEX(010C0C0C0C0C):PRINT TAB (56);"B !* R":SELECT PRINT 005(255),P6
331 PRINT TAB(56); HEX(5C552021452F0808080808080A2D200909202D0D01);"SORRY CHARLIE...":PRINT HEX(03);: SELECT PRINT 005(64),P1
340 FOR I=1 TO 3: PRINT TAB(31);"+": NEXT I: PRINT TAB(21);"+ + + + + + + + + + +":FOR I=1 TO 8:PRINT TAB(31);"+":NEXT I: SELECT P
350 PRINT "NOCHMAL ?";
360 KEYIN Q$,370,370:GOTO 360
370 IF Q$<>"1" THEN 380:GOTO 10
380 IF Q$<>"0" THEN 360
