CLEAR
5REM WRITTEN BY 2200 R & D -- FEB. 22, 1974
10DIM S(3),S1(3,10),S1$20
20PRINT HEX(03);:PRINT ,"***WANG 2200 FOOTBALL***"
30D$=HEX(010A0A0A0A0A0A0A0A0A0A0A):GOTO 1690
40%          G....!....1....!....2....!....3....!....4....!....M
50%          M....!....4....!....3....!....2....!....1....!....G
60%!     #    -##.#             #      ##.##      !
70DEFFN1(X)=8*A*(RND(1)-.5)^3+B
80DEFFN'32(E1):FOR E=1 TO E1:NEXT E:RETURN 
90DEFFN'33(E):PRINT STR(D$,1,E);
100PRINT TAB(63);HEX(0D);:RETURN 
110DEFFN'30(E1):S1(2-P,E1)=S1(2-P,E1)+1:RETURN 
120DEFFN'31(E1):S1(2-P,E1)=S1(2-P,E1)+INT(Y):RETURN 
130DEFFN'15:PRINT "CALL PLAYS AS FOLLOWS:"
140PRINT " 1=SIMPLE RUN   2=TRICKY RUN   3=SHORT PASS   4=LONG PASS"
150PRINT " 5=PUNT   6=QUICK KICK   7=PLACE KICK":RETURN 
160T=T+RND(1):IF T<30 THEN 180
170IF H2=4THEN 2010:GOTO 1190
180IF T>=28THEN 200:IF H7-T>0THEN 220
190T=15:H7=30:H2=H2+1:GOTO 220
200IF H1=1THEN 220
210GOSUB 1340:GOSUB 1500:PRINT "TWO MINUTE WARNING",:H1=1:T=28
220GOSUB 1380:Y,F=0:IF P=1 THEN 300:GOTO 1020
230A=20:B=0:X=FN1(X)
240GOSUB 1500:PRINT "KICKOFF...",:GOSUB 1340:K=1
250Y=0:F=-1:Z=8:X6=60:GOSUB 1530:GOSUB 1510
260K=0:IF X>2 THEN 280:PRINT " TOUCHBACK."
270X6,X=20:GOSUB 1530:GOTO 880
280GOSUB 1270:GOTO 740
290GOSUB '33(8):PRINT "MISCALLED PLAY. CALL PLAY AGAIN.";
300IF G=3THEN 1030
310GOSUB '33(12):GOSUB '15:GOSUB 100:GOSUB 1350
320INPUT "'S BALL.  NEXT PLAY",L: Z=ABS(INT(L))
330IF Z=0THEN 290:IF Z>7THEN 290
340DEFFN'14:R=RND(1):GOSUB '33(8):GOSUB '30(10):J=J+1
350IF RND(1)>.005THEN 380:PRINT "TIME OUT...  DOG ON THE FIELD"
360PRINTUSING 40:S1$="           DOG":PRINT 
370PRINT :PRINT STR(D$,1,11):GOSUB 1460
380IF RND(1)<.06THEN 1280:IF Z>4THEN 570:IF Z<3THEN 400
390PRINT "PASS.";:GOSUB '30(4):IF Z=3THEN 470:GOTO 540
400PRINT "RUN.";:IF Z<>1THEN 420
410A=4.1:B=3:Y=FN1(X):IF R<.05THEN 430:GOTO 460
420A=15:B=6:Y=FN1(X):IF R>.15THEN 460
430PRINT "***FUMBLE***";:IF RND(1)<=.4THEN 450
440PRINT " BALL LOST";:F=-1:GOSUB '30(9):GOSUB 1270:GOTO 830
450PRINT " BALL RECOVERED WITH";:GOTO 830
460GOSUB '31(1):GOTO 830
470IF R>=.4THEN 480:IF R>.1THEN 530:GOTO 520
480A=7.6:B=8:Y=FN1(X):IF R>.45THEN 560
490F=-1:GOSUB 1520:PRINT "INTERCEPTED!";:GOSUB '30(9)
500IF X<100THEN 510:PRINT "IN THE ENDZONE.":GOSUB 1340:X=80
510GOSUB 1270:GOTO 880
520PRINT " BLITZ!  PASSER TACKLED.";:Y=-13*RND(1):GOTO 830
530PRINT " INCOMPLETE.";:Y=0:GOTO 830
540IF R<.15THEN 520:IF R<.6THEN 530
550A=20:B=30:Y=FN1(X):IF R<.7THEN 490
560GOSUB '31(2):GOSUB '30(3):GOTO 830
570F=-1:K=1:IF Z=5THEN 580:IF Z=6THEN 590:GOTO 600
580 A=15:B=40:GOTO 610
590A=20:B=30:GOTO 610
600A=20:B=35
610Y=FN1(X):IF Z=7THEN 780
620PRINT "PUNT.";:GOSUB '30(6):GOSUB '31(5)
630IF X+Y>=100THEN 640:IF R<.7THEN 660:PRINT "FAIR CATCH CALLED."
640GOSUB 1520:IF X<100THEN 650:PRINT "TOUCHBACK":X=80
650GOSUB 1270:K=0:GOTO 880
660IF Z=5THEN 680
670Y2=(3*R)^3+R^2*25:IF Z>=7THEN 710:GOTO 690
680Y2=(3*R)^3+2*R
690GOSUB 1520:GOSUB 1510
700 IF Z<>5THEN 710: PRINT "GOOD FOR";INT(Y);
710K=0:GOSUB 1270:X6=X:X=X+Y2:GOSUB 1530
720PRINT "RUNBACK OF";INT(Y2);"YARDS":GOTO 880
730GOSUB 1510:PRINT "SHORT. ";
740PRINT "BALL TAKEN ON THE";:IF INT(X)=0THEN 760
750PRINT INT(100-X);:GOTO 670
760PRINT "GOAL LINE";:GOTO 670
770GOSUB 1270:PRINT "MISSED. ":K=0 :GOTO 270
780PRINT "PLACEKICK.";:IF R>=.15THEN 800
790PRINT "BLOCKED.":Y=-5:F,K=0:GOTO 830
800GOSUB 1520:IF X<100THEN 730:IF X<105THEN 770
810IF RND(1)<.15THEN 770:I=3:GOSUB 1260:S1$="***FIELD GOAL***":GOSUB 1450
820GOSUB 1270:GOTO 230
830GOSUB 1520:IF Y=0THEN 850:IF Y<0THEN 860
840PRINT "GAIN OF ";INT(Y+.5):GOTO 870
850PRINT "NO GAIN ":GOTO 870
860PRINT "LOSS OF ";INT(ABS(Y)+.5):IF X<=0THEN 960:GOTO 880
870IF X>=100THEN 970
880IF F=0THEN 890:X6=X:GOSUB 1530:GOTO 920
890X2=X-X1:IF X2>=10THEN 920
900D=D+1:IF D<5THEN 160:GOSUB 1270:GOSUB 1340:GOSUB 1500
910PRINT "FOURTH DOWN ATTEMPT NO GOOD!":F=-1:GOSUB 1340:GOTO 880
920D=1:X2=0:IF J1=0THEN 930:J2=X:J1=0
930IF Z>4THEN 940:GOSUB '30(7)
940IF X>90THEN 950:X1=X:GOTO 160
950X1=90:X2=X-X1:GOTO 160
960P=-P:I=2:GOSUB 1260:S1$="***SAFETY***":GOSUB 1450:GOTO 230
970I=6:GOSUB 1260:IF J=0THEN 990
980GOSUB '33(8):PRINT "TOUCHDOWN DRIVE WAS";INT(100-J2);"YARDS IN";J;"PLAYS.":S1$="***TOUCHDOWN***":GOSUB 1450:GOSUB '33(8):GOSUB 100
990IF R>.9THEN 1000:PRINT "KICK IS GOOD. ":I=1:GOSUB 1260:GOTO 1010
1000PRINT "CONVERSION FAILS"
1010GOSUB 1270:GOSUB 1340:PRINT HEX(0C);:GOTO 230
1020IF G=2THEN 310
1030GOSUB '32(650):GOSUB '33(12):X2=10-X+X1:R=RND(1)
1040GOSUB 1350:PRINT "'S BALL.":PRINT TAB(63):PRINT TAB(63):PRINT TAB(63)
1050IF T<29.5THEN 1060:IF X<55THEN 1160:GOTO 1180
1060IF D=4THEN 1110:IF D=3THEN 1080:IF X>90THEN 1130
1070IF R<.2THEN 1160:IF R<.65THEN 1150:IF R<.9THEN 1140:GOTO 1130
1080IF X2<2THEN 1130:IF X2>15THEN 1100
1090IF X>95THEN 1130:IF R<.4THEN 1130:IF R<.9THEN 1140:GOTO 1150
1100IF R<.4THEN 1140:IF R<.8THEN 1150:GOTO 1160
1110IF T<25THEN 1120:IF H2=2THEN 1120:IF S(2+P)<S(2-P)+3THEN 1120:IF X2<2THEN 1130:GOTO 1150
1120IF X<=60THEN 1170:GOTO 1180
1130Z=1:GOTO 340
1140Z=2:GOTO 340
1150Z=3:GOTO 340
1160Z=4:GOTO 340
1170Z=5:GOTO 340
1180Z=7:GOTO 340
1190GOSUB 1340:PRINT HEX(03);:T=30:D=1:X2=0:GOSUB 1380
1200PRINT ,"***HALF TIME***":PRINT :P=-H
1210PRINTUSING 50:S1$="           BAND":PRINT 
1220PRINT :PRINT STR(D$,1,11):GOSUB 1460:GOSUB '2("<<<<<HALF TIME>>>>>>")
1230GOSUB '32(4000):GOSUB 1350
1240PRINT " WILL RECEIVE TO OPEN THE SECOND HALF";:GOSUB '32(2000)
1250H2=3:H,T,H1=0:H7=15:PRINT HEX(03);:GOSUB 1380:GOTO 230
1260S(2-P)=S(2-P)+I:GOSUB 1380:RETURN 
1270P=-P:X=100-X:J=0:J1=1:RETURN 
1280Y=-5:IF RND(1)>.3THEN 1290:Y=5
1290GOSUB 1300:PRINT " OFF SIDES.  ";:GOTO 830
1300PRINT "***PENALTY***";:D=D-1:GOSUB '30(8)
1310IF Y>0THEN 1320:PRINT " OFFENSE ";:IF X>ABS(Y)THEN 1330:Y=-X/2:RETURN 
1320PRINT " DEFENSE ";:IF 100-X>YTHEN 1330:Y=50-X/2
1330RETURN 
1340GOSUB '32(400):RETURN 
1350IF P=1THEN 1360:PRINT A$;:RETURN 
1360PRINT B$;:RETURN 
1370%<<<<<<<<<<<<<<####################>>>>>>>>>>>>>>
1380PRINT :PRINT HEX(01);
1390PRINTUSING 1370,"WANG 2200 SCOREBOARD"
1400PRINT "!  ";A$;S(3);TAB(24);B$;S(1);TAB(47);"!"
1410PRINT "!";TAB(47);"!"
1420PRINT "!   DOWN  YDS.TO GO       PERIOD    TIME       !"
1430PRINTUSING 60,D,10-X2,H2,INT(H7-T)+.6*(H7-T-INT(H7-T))
1440PRINTUSING 1370,"<<<<<DRINK COKE>>>>>":PRINT :RETURN 
1450PRINT :PRINT STR(D$,1,3);
1460FOR E=1 TO 10:PRINT HEX(07):FOR E1=1 TO 30
1470PRINT "!";TAB(E1);S1$;" ";HEX(0D);:NEXT E1:PRINT HEX(07)
1480FOR E1=30TO 1 STEP -1
1490PRINT "!";TAB(E1);S1$;" ";HEX(0D);:NEXT E1:NEXT E:GOSUB 1380
1500GOSUB '33(8):RETURN 
1510PRINT STR(D$,1,11);HEX(0D);TAB(13);HEX(0C0C0C);:RETURN 
1520X6=X:X=X+Y
1530PRINT STR(D$,1,9);HEX(0D);:IF INT(X)=INT(X6)THEN 1570
1540IF INT(X)=INT(X6)THEN 1570
1550IF X>X6THEN 1560:X6=X6-1-K:GOTO 1570
1560X6=X6+1+K
1570IF P=-1THEN 1590:X3=100-X6
1580IF X6>=50THEN 1610:GOTO 1600
1590X3=X6:IF X6<=50THEN 1610
1600PRINTUSING 50:X3=X3-50:GOTO 1620
1610PRINTUSING 40
1620PRINT TAB(63);HEX(0D);TAB(X3+10);
1630IF K=0THEN 1640:PRINT "O":GOTO 1660
1640IF P=-1THEN 1650:PRINT "<O":GOTO 1660
1650PRINT "O>"
1660IF INT(X)=INT(X6)THEN 1680:IF INT(X)=INT(X6)+1THEN 1680
1670IF X3<0THEN 1680:IF X3>50THEN 1680:PRINT HEX(0C0C):GOTO 1550
1680PRINT TAB(43);HEX(0C0C0C);:RETURN 
1690PRINT "SET-UP PROCEDURE"
1700A$="OPPONENT":PRINT A$;:INPUT A$:B$="YOUR TEAM":PRINT B$;:INPUT B$:PRINT 
1710PRINT "THERE ARE THREE WAYS TO PLAY:"
1720PRINT ,"1. COMPUTER VS. PLAYER":PRINT ,"2. PLAYER VS. PLAYER":PRINT ,"3. COMPUTER VS. COMPUTER"
1730INPUT "HOW DO YOU WISH TO PLAY",G
1740 PRINT HEX(03);"TODAY'S GAME PITS ";A$;" VS. ";B$:PRINT 
1750IF G<>3THEN 1760:PRINT "THE 2200 WILL SELECT AND ";:GOTO 1770
1760PRINT "YOU WILL CALL PLAYS FOR YOUR TEAM"
1770 GOSUB '15
1780PRINT :PRINT "TO START THE GAME, PRESS 'HALT/STEP'":PRINT ,"AND THEN 'S.F.KEY 0'"
1790R=RND(1):GOTO 1790
1800DEFFN'0:H1,T,J=0:H2,D,P,J1=1:H7=15
1810IF R<.5THEN 1820:P=-1
1820GOSUB 1350:H=P:PRINT " WON THE TOSS AND WILL RECEIVE."
1830GOSUB 1340:PRINT HEX(03);:GOSUB 1380:GOTO 230
1840DEFFN'2(S1$):PRINT :PRINT HEX(03):PRINTUSING 1370,"WANG 2200 SCOREBOARD"
1850PRINT "!  ";A$;S(3);TAB(24);B$;S(1);TAB(47);"!"
1860PRINTUSING 1370,S1$:PRINT 
1870PRINT "YDS. RUSHING",;:GOSUB '29(1)
1880PRINT "YDS. PASSING",;:GOSUB '29(2)
1890PRINT "TOTAL OFFENSE",S1(3,1)+S1(3,2),S1(1,1)+S1(1,2):PRINT 
1900PRINT "TOTAL # PLAYS",;:GOSUB '29(10)
1910PRINT "FIRST DOWNS",;:GOSUB '29(7):GOSUB '20(S1(3,4),S1(1,4))
1920PRINT "% PASS COMPL.",INT(100*S1(3,3)/E),INT(100*S1(1,3)/E1)
1930GOSUB '20(S1(3,6),S1(1,6))
1940PRINT "AVG.YDS.PUNT",INT(S1(3,5)/E),INT(S1(1,5)/E1):PRINT 
1950PRINT "PENALTIES",;:GOSUB '29(8)
1960PRINT "TURNOVERS",;:GOSUB '29(9):RETURN 
1970DEFFN'29(E):PRINT S1(3,E),S1(1,E):RETURN 
1980DEFFN'20(E,E1):IF E>0THEN 1990:E=1
1990IF E1>0THEN 2000:E1=1
2000RETURN 
2010GOSUB 1340:GOSUB '2("<<<<<FINAL SCORE>>>>")
2020IF S(3)=S(1)THEN 2080:IF S(3)>S(1)THEN 2040
2030GOSUB 2050:PRINT "!  ";A$;S(3);TAB(47);"!";:GOTO 2030
2040GOSUB 2050:PRINT "!  ";TAB(24);B$;S(1);TAB(47);"!";:GOTO 2040
2050PRINT HEX(010D0A);:GOSUB '32(50)
2060PRINT "!  ";A$;S(3);TAB(24);B$;S(1);TAB(47);"!"
2070PRINT HEX(010D0A);:GOSUB '32(100):RETURN 
2080REM 
