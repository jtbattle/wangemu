CLEAR

10 %       "RATTE" - VON MATTHIAS MUTH
20 SELECT PRINT 205
30 DIM A$(7,7)4,B$(2)7,C$64,C$(7,7)4,D$4
40 DIM D$(2)35
50 DEFFN'00"LIST S"
60 INIT(30)A$():INIT(09)C$:INIT(0A)L$:STR(L$,1,1)=HEX(01)
70 FOR A=1 TO 7:STR(A$(7,A),1,1),STR(A$(A,1),2,1)="1"
80 STR(A$(A,7),3,1),STR(A$(1,A),4,1)="1":NEXT A
90 B$(1)=HEX(490A08490A0849):B$(2)="I---I"
100 GOSUB 470
110 FOR R=3 TO 13 STEP 2:FOR S=6 TO 26 STEP 4:GOSUB'32(R,S)
120 PRINT "+":NEXT S:NEXT R
130 R1=1:S1=7:GOSUB '32(2*R1,4*S1):PRINT "Z"
140 R0=7:S0=1:GOSUB '32(2*R0,4*S0):PRINT "A"
150 GOTO 190
160 DEFFN'32(L,C):PRINT STR(L$,1,L);STR(C$,1,C);:RETURN
170 DEFFN'13
180 GOSUB 470
190 FOR R=1 TO 7
200 FOR S=1 TO 7
210 GOSUB'32(2*R,4*S):PRINT "0"
220 KEYIN A$,230,220:GOTO 220
230 IF A$<>"Z" THEN 250:GOSUB '32(2*R1,4*S1):PRINT " ":R1=R
240 S1=S:GOSUB '32(2*R1,4*S1):PRINT A$:GOTO 220
250 IF A$<>"A" THEN 270:GOSUB '32(2*R0,4*S0):PRINT " ":R0=R
260 S0=S:GOSUB '32(2*R0,4*S0):PRINT A$:GOTO 220
270 IF NUM(A$)=0 THEN 220:CONVERT A$ TO A:A=A/2
280 IF A<>INT(A) THEN 220
290 IF A=0 THEN 400
300 STR(A$(R,S),A,1)="1"
310 Y=2*R-SGN(A-1.5):X=4*S+2*SGN(-ABS(A-3)+.5)
320 B=ABS(A-2.5)+.5
330 GOSUB '32(Y,X):PRINT B$(B)
340 Y=R-INT((A/2)-1)
350 X=S+SGN(A-2.5)*1.5-(A-2.5)
360 A1=5-A
370 IF X<1 THEN 220:IF Y<1 THEN 220
380 IF X>7 THEN 220:IF Y>7 THEN 220
390 STR(A$(Y,X),A1,1)="1":GOTO 220
400 GOSUB '32(2*R,4*S):PRINT " "
410 GOSUB '32(2*R1,4*S1):PRINT "Z"
420 GOSUB '32(2*R0,4*S0):PRINT "A"
430 NEXT S:NEXT R
440 DEFFN'14
450 FOR R=1 TO 7:FOR S=1 TO 7:C$(R,S)=A$(R,S):NEXT S:NEXT R
460 DEFFN'15:GOSUB 470:GOTO 560
470 PRINT HEX(03)
480 FOR R=1 TO 7:FOR S=1 TO 7:FOR A=1 TO 4
490 IF STR(A$(R,S),A,1)="0" THEN 540
500 GOSUB 510:GOTO 530
510 Y=2*R-SGN(A-1.5):X=4*S+2*SGN(-ABS(A-3)+.5):B=ABS(A-2.5)+.5
520 RETURN
530 GOSUB '32(Y,X):PRINT B$(B)
540 NEXT A:NEXT S:NEXT R
550 RETURN
560 R=R0:S=S0:A0=3
570 V=0
580 GOSUB '32(2*R1,4*S1):PRINT "Z"
590 D$=C$(R,S):STR(D$,5-A0,1)="1":N=0
600 FOR A=1 TO 4:IF STR(D$,A,1)="1" THEN 610:N=N+1
610 NEXT A
620 IF N<>0 THEN 630:A=5-A0:GOTO 670
630 A=POS(D$="0"):A0=A
640 IF N=1 THEN 670
650 R2=R:S2=S:V=V+1
660 A=INT(RND(1)*4)+1:IF STR(D$,A,1)="1" THEN 660
670 GOSUB '32(2*R,4*S):PRINT " "
680 A0=A
690 R=R-INT((A/2)-1):S=S+SGN(A-2.5)*1.5-(A-2.5)
700 GOSUB '32(2*R,4*S):PRINT "@"
710 IF R<>R1 THEN 760:IF S<>S1 THEN 760
720 FOR P=1 TO 200:NEXT P:PRINT HEX(03),"GESCHAFFT !";
730 IF V>0 THEN 750:PRINT " (KUERZESTER WEG !!)"
740 PRINT HEX(0A0A)
750 STOP "DEFFN'"
760 IF R<>R2 THEN 590:IF S<>S2 THEN 590
770 STR(C$(R,S),5-A,1)="1":GOTO 590
780 SELECT PRINT 211
790 FOR S=1 TO 7:STR(D$(1),4*S-2,5)="I---I":NEXT S:PRINT D$(1)
800 FOR R=1 TO 7:INIT(20)D$()
810 FOR S=1 TO 7:FOR A=1 TO 3
820 IF STR(A$(R,S),A,1)="0" THEN 850
830 GOSUB 510:IF B=1 THEN 840:STR(D$(2),X,5)=B$(2):GOTO 850
840 FOR C=1 TO 2:STR(D$(C),X,1)="I":NEXT C
850 NEXT A:NEXT S:PRINT D$(1):PRINT D$(2):NEXT R
860 SELECT PRINT 205
